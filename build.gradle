plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'eu.kk42'
version '0.2.1-SNAPSHOT'

sourceCompatibility = 11


repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.19.1'
    implementation group: 'net.sourceforge.cssparser', name: 'cssparser', version: '0.9.30'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.17'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.12.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.12.0'
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.24.3'
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.24.3'
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'mail-preflight'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Mail Preflight'
                description = 'Library for preprocessing emails HTML (css inlining, comments removal etc.)'
                url = 'https://github.com/kkonstantin42/mail-preflight'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kk42'
                        name = 'Konstantin Kastanov'
                        email = 'konstantin.kastanov@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/kkonstantin42/mail-preflight.git'
                    developerConnection = 'scm:git:ssh://github.com/kkonstantin42/mail-preflight.git'
                    url = 'https://github.com/kkonstantin42/mail-preflight'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username =  project.findProperty('nexusUsername') ?: 'secured'
                password =  project.findProperty('nexusPassword') ?: 'secured'
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '5.3.1'
}